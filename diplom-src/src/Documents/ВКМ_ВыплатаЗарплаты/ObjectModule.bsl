#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();
	
	Для Каждого Строка Из Выплаты Цикл
		ВзаиморасчетыПоСотруднику = РегистрыНакопления.ВКМ_ВзаиморасчетыССотрудниками.ВзаиморасчетыПоСотруднику(Строка.Сотрудник, Ссылка);
		Если ВзаиморасчетыПоСотруднику <= 0 Тогда
			Текст = СтрШаблон("У сотрудника %1 недостаточно начислений для выплаты зарплаты", Строка.Сотрудник);
			ОбщегоНазначения.СообщитьПользователю(Текст);
			Отказ = Истина;
			Возврат;
		Иначе
			Если ВзаиморасчетыПоСотруднику < Строка.Сумма Тогда
				Строка.Сумма = ВзаиморасчетыПоСотруднику;
				Текст = СтрШаблон("У сотрудника %1 заменена суммы выплаты в соответствии с начислениями", Строка.Сотрудник);
				ОбщегоНазначения.СообщитьПользователю(Текст);
			КонецЕсли;
		КонецЕсли;
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Сумма = Строка.Сумма;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли