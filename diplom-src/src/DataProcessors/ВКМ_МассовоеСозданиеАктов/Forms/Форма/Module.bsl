
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьДоговоры(Команда)
	
	ЗаполнитьДоговорыФоном();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРеализации(Команда)
	
	СформироватьРеализацииФоном();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДоговорыФоном()
	
	Если Период = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо выбрать период заполнения");
		Возврат;
	КонецЕсли;
		
	СтруктураФоновогоЗадания = ВыполнитьФоновоеЗаполнениеДоговоров(УникальныйИдентификатор);
	Если СтруктураФоновогоЗадания = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Загрузка уже выполняется"); 
		Возврат;
	КонецЕсли;    
	
	ИДЗадания 	= СтруктураФоновогоЗадания.ИдентификаторЗадания;
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	//ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.Интервал 	= 2;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = "Пожалуйста, подождите, идёт загрузка данных.";
	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("ИДЗадания", ИДЗадания);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания, Новый ОписаниеОповещения("ПослеЗавершенияПолученияДоговоров", ЭтотОбъект, 
		ДополнительныеПараметрыОповещения), ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияПолученияДоговоров(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Таблица.Очистить();
	Если Результат.Статус = "Выполнено" Тогда
		МассивДоговоров = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Для Каждого ЭлементМассива Из МассивДоговоров Цикл
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Договор = ЭлементМассива;
		КонецЦИкла;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка заполнения, обратитесь к админинстратору");
	КонецЕсли;
	Модифицированность = Ложь;

КонецПроцедуры

&НаСервере
Функция ВыполнитьФоновоеЗаполнениеДоговоров(УникальныйИдентификатор)
		
	СтруктураФоновогоЗадания = Неопределено;
	НаименованиеЗадания = "Заполнение абонентских договоров за период";
	//Функция Кирилла 
	ВыполняемыйМетод = "ВКМ_ОбработкаФоновыхЗаданий.АбонентскиеДоговорыЗаПериод";   
	
	Если Не ВКМ_ОбработкаФоновыхЗаданий.ФоновоеЗаданиеАктивно(НаименованиеЗадания) Тогда 
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
		СтруктураФоновогоЗадания = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ВыполняемыйМетод, Период.ДатаНачала, Период.ДатаОкончания); 
	КонецЕсли;   
	
	Возврат СтруктураФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура СформироватьРеализацииФоном()
			
	СтруктураФоновогоЗадания = ВыполнитьФоновоеСозданиеРеализаций(УникальныйИдентификатор);
	Если СтруктураФоновогоЗадания = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Загрузка уже выполняется"); 
		Возврат;
	КонецЕсли;    
	
	ИДЗадания 	= СтруктураФоновогоЗадания.ИдентификаторЗадания;
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	//ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.Интервал 	= 2;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = "Пожалуйста, подождите, идёт обработка данных.";
	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("ИДЗадания", ИДЗадания);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания, Новый ОписаниеОповещения("ПослеЗавершенияСозданияРеализаций", ЭтотОбъект, 
		ДополнительныеПараметрыОповещения), ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьФоновоеСозданиеРеализаций(УникальныйИдентификатор)
	
	ТаблицаЗначений = РеквизитФормыВЗначение("Таблица");
	МассивДоговоров = ТаблицаЗначений.ВыгрузитьКолонку("Договор");
		
	СтруктураФоновогоЗадания = Неопределено;
	НаименованиеЗадания = "Создание реализаций по договорам";
	//Функция Кирилла 
	ВыполняемыйМетод = "ВКМ_ОбработкаФоновыхЗаданий.СозданиеРеализацийПоДоговорам";   
	
	Если Не ВКМ_ОбработкаФоновыхЗаданий.ФоновоеЗаданиеАктивно(НаименованиеЗадания) Тогда 
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
		СтруктураФоновогоЗадания = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ВыполняемыйМетод, МассивДоговоров, Период); 
	КонецЕсли;   
	
	Возврат СтруктураФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияСозданияРеализаций(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Таблица.Очистить();
	Если Результат.Статус = "Выполнено" Тогда
		МассивСтруктур = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Для Каждого ЭлементМассива Из МассивСтруктур Цикл
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
		КонецЦИкла;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка заполнения, обратитесь к админинстратору");
	КонецЕсли;
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти
